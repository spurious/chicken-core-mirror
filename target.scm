;;; cgen.scm - C code-generator for the CHICKEN compiler
;
; Copyright (c) 2018, The CHICKEN Team
; All rights reserved.
;
; Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following
; conditions are met:
;
;   Redistributions of source code must retain the above copyright notice, this list of conditions and the following
;     disclaimer. 
;   Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following
;     disclaimer in the documentation and/or other materials provided with the distribution. 
;   Neither the name of the author nor the names of its contributors may be used to endorse or promote
;     products derived from this software without specific prior written permission. 
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS
; OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY
; AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDERS OR
; CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
; SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
; THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
; OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
; POSSIBILITY OF SUCH DAMAGE.


(declare
  (unit target)
  (uses data-structures extras c-platform compiler internal support))

(module chicken.compiler.target
    (init-target finalize-target generate-target-code)

(import scheme
	chicken.base
	chicken.format
        chicken.fixnum
	chicken.internal
	chicken.platform
	chicken.string
        chicken.pretty-print
	chicken.compiler.core
	chicken.compiler.c-platform
	chicken.compiler.support)

(include "mini-srfi-1.scm")


(define (backslashify s)
  (string-translate* (->string s) '(("\\" . "\\\\"))))

(define (uncommentify s)
  (string-translate* (->string s) '(("*/" . "*_/"))))

(define output #f)

(define (emit . xs)
  (for-each 
    (lambda (x)
      (case x
        ((#t) (newline output))
        ((#f) #f)
        (else (display x output))))
    xs))

(define (emit-list xs proc)
  (let loop ((xs xs))
    (unless (null? xs)
      (proc (car xs))
      (unless (null? (cdr xs))
        (emit ",")
        (loop (cdr xs))))))

(define (header opts src)
  (emit "/* Generated from " src " by the CHICKEN compiler" #t
       "   http://www.call-cc.org" #t
       (string-intersperse
         (map (cut string-append "   " <> "\n")
            (string-split (chicken-version #t) "\n") )
	    "")
	   "   command line: ")
      (foreach (cut emit <> " ") opts)
      (unless (not unit-name)
	(emit #t "   unit: " unit-name))
      (unless (null? used-units)
	(emit #t "   uses: ")
	(emit-list used-units emit))
      (emit #t "*/")
      (emit #t "#include \"" target-include-file "\""))

(define (trailer)
  (emit #t #t "/*" #t 
       (uncommentify
         (get-output-string
            collected-debugging-output))
       "*/" #t "/* end of file */" #t))

(define (init-target out user-options source-file)
  (set! output out)
  (header user-options source-file))

(define (finalize-target)
  (trailer))

(define (generate-target-code form)
  (when (memq 'y debugging-chicken)
    (flush-output)
    (pp form))
  (top-expr form))

(define (top-expr x)
  (case (and (pair? x) (car x))
    ((begin)
     (for-each top-expr (cdr x)))
    ((call tailcall)
     (cond ((atom? (cadr x)) (emit #t (cadr x)))
           (else
             (emit #t "(")
             (expr (cadr x))
             (emit ")")))
     (emit "(")
     (emit-list (cddr x) expr)
     (emit ");"))
    ((case)
     (emit #t "case ")
     (expr (cadr x))
     (emit ":"))
    ((comment)
     (emit #t "/* " (uncommentify (cadr x)) " */"))
    ((declare)
     (let ((class "")
           (noret ""))
       (let loop ((x (cdr x)))
         (case (car x)
           ((extern) 
            (set! class 'extern)
            (loop (cdr x)))
           ((static) 
            (set! class 'static)
            (loop (cdr x)))
           ((noreturn) 
            (set! noret "C_noret")
            (loop (cdr x)))
           (else
             (emit #t class " ")
             (type (car x))
             (emit " " (cadr x) "(")
             (emit-list (cddr x)
                        (lambda (arg)
                          (type (car arg))
                          (emit " " (cadr arg))))
             (emit ") " noret ";"))))))
    ((declare/array)
     (let ((class "")
           (aligned ""))
       (let loop ((x (cdr x)))
         (case (car x)
           ((static) 
            (set! class 'static)
            (loop (cdr x)))
           ((aligned)
            (set! aligned "C_aligned")
            (loop (cdr x)))
           (else
             (emit #t class " ")
             (type (car x))
             (emit " " (cadr x) "[")
             (unless (null? (caddr x)) (emit (caddr x)))
             (emit "] " aligned ";"))))))
    ((define)
     (let ((class "")
           (noreturn ""))
       (let loop ((x (cdr x)))
         (case (car x)
           ((extern) 
            (set! class 'extern)
            (loop (cdr x)))
           ((static)
            (set! class 'static)
            (loop (cdr x)))
           ((noreturn)
            (set! noreturn "C_noret")
            (loop (cdr x)))
           (else
             (emit #t class " ")
             (type (car x))
             (emit " " (cadr x) "(")
             (emit-list (cddr x)
                        (lambda (arg) 
                          (type (car arg))
                          (emit " " (cadr arg))))
             ;; tmp var for box/closure allocation
             (emit ") " noreturn "{" #t "C_word tmp;"))))))
    ((end) (emit "}"))
    ((define/array)
     (let ((class "")
           (aligned ""))
       (let loop ((x (cdr x)))
         (case (car x)
           ((static)
            (set! class 'static)
            (loop (cdr x)))
           ((aligned)
            (set! aligned "C_aligned")
            (loop (cdr x)))
           (else
             (emit #t class " ")
             (type (car x))
             (emit " " (cadr x) "[")
             (unless (null? (caddr x)) (emit (caddr x)))
             (emit "] " aligned " ={")
             (let loop ((x (cdddr x)))
               (unless (null? x)
                 (let ((val (car x)))
                   (cond ((vector? val)
                          (emit #t "{")
                          (emit-list (vector->list val) expr)
                          (emit "},"))
                         ((and (pair? val) (eq? 'C_lihdr (car val)))
                          (let ((b1 (cadr val))
                                (b2 (caddr val))
                                (b3 (cadddr val)))
                            (emit "C_lihdr(" b1 "," b2 "," b3 "),")))
                         (else (emit val ",")))
                   (loop (cdr x)))))
             (emit "};"))))))
    ((define/variable)
     (let ((class ""))
       (when (memq (cadr x) '(static extern))
         (set! class (cadr x))
         (set! x (cdr x)))
       (emit #t class " ")
       (type (cadr x))
       (emit " " (caddr x))
       (unless (null? (cdddr x))
         (emit "=" (cadddr x)))
       (emit ";")))
    ((goto)
     (emit #t "goto " (cadr x)))
    ((if)
     (emit #t "if(")
     (expr (cadr x))
     (emit ") {")
     (top-expr (caddr x))
     (unless (null? (cdddr x))
       (emit "}" #t "else{")
       (top-expr (cadddr x)))
     (emit "}"))
    ((inline)
     (emit #t (cadr x)))
    ((label)
     (emit #t (cadr x) ":;"))
    ((let)
     (emit #t "C_word " (cadr x))
     (unless (null? (cddr x))
       (emit "=")
       (expr (caddr x)))
     (emit ";"))
    ((let/proc)
     (emit #t "C_proc " (cadr x) "=")
     (expr (caddr x))
     (emit ";"))
    ((let/ptr)
     (emit #t "C_word* " (cadr x))
     (unless (null? (cddr x))
       (emit "=")
       (expr (caddr x)))
     (emit ";"))
    ((let/array)
     (emit #t "C_word " (cadr x) "[")
     (expr (caddr x))
     (emit "];"))
    ((let/unboxed)
     (emit #t)
     (type (cadr x))
     (emit " " (caddr x))
     (unless (null? (cdddr x))
       (emit "=")
       (expr (cadddr x))
       (emit ";")))
    ((return)
     (emit #t "return ")
     (expr (cadr x))
     (emit ";"))
    ((set)
     (emit #t)
     (expr (cadr x))
     (emit "=")
     (expr (caddr x))
     (emit ";"))
    ((switch)
     (emit #t "switch(")
     (expr (cadr x))
     (emit "){"))
    ((default)
     (emit #t "default:;"))
    ((endswitch)
     (emit "}"))
    (else (bomb "bad top-expr" x))))

(define (expr-list xs)
  (let loop ((xs xs))
    (unless (null? xs)
      (expr (car xs))
      (unless (null? (cdr xs))
        (emit ",")
        (loop (cdr xs))))))

(define (expr x)
  (cond ((string? x) (emit "\"" (backslashify x) "\""))
        ((atom? x) (emit x))
        (else
          (case (car x)
            ((adr) (emit "&" (cadr x)))
            ((box) 
             (let ((aexp (cadr x)))
               (emit "(*")
               (expr aexp)
               (emit "=C_VECTOR_TYPE|1,")
               (expr aexp)
               (emit "[1]=")
               (expr (caddr x))
               (emit ",tmp=(C_word)")
               (expr aexp)
               (emit ",")
               (expr aexp)
               (emit "+=2,tmp)")))
            ((cast)
             (emit "(")
             (type (car x))
             (emit ")")
             (expr (cadr x)))
            ((closure)
             (let ((size (cadr x))
                   (aexp (caddr x)))
               (emit "(*")
               (expr aexp)
               (emit "=C_CLOSURE_TYPE|" size ",")
               (for-each
                 (lambda (arg i)
                   (expr aexp)
                   (emit "[" i "]=")
                   (expr arg)
                   (emit ","))
                 (cdddr x) 
                 (list-tabulate size add1))
               (emit "tmp=(C_word)")
               (expr aexp)
               (emit ",")
               (expr aexp)
               (emit "+=" (add1 size) ",tmp)")))
            ((cond)
             (emit "(")
             (expr (cadr x))
             (emit "?")
             (expr (caddr x))
             (emit ":")
             (expr (cadddr x))
             (emit ")"))
            ((deref)
             (emit "*")
             (expr (cadr x)))
            ((elt)
             (expr (cadr x))
             (emit "[")
             (expr (caddr x))
             (emit "]"))
            ((inline)
             (emit (cadr x)))
            ((mutate)
             (emit "C_mutate((C_word *)(")
             (expr (cadr x))
             (emit ")+")
             (expr (caddr x))
             (emit ",")
             (expr (cadddr x))
             (emit ")"))
            ((setslot)
             (emit "(((C_SCHEME_BLOCK *)")
             (expr (cadr x))
             (emit ")->data[")
             (expr (caddr x))
             (emit "]=" )
             (expr (cadddr x))
             (emit ")"))
            ((slot)
             (emit "(((C_SCHEME_BLOCK *)")
             (expr (cadr x))
             (emit ")->data[")
             (expr (caddr x))
             (emit "])"))
            ((unlikely)
             (emit "C_unlikely(")
             (expr (cadr x))
             (emit ")"))
            ((words)
             (emit "(")
             (expr (cadr x))
             (emit "*sizeof(C_word))"))
            ((~ !)
             (emit "(" (car x))
             (expr (cadr x))
             (emit ")"))
            ((+ - * / > < >= <= == != & ^ |\||)
             (emit "(")
             (cond ((null? (cddr x))
                    (emit (car x))
                    (expr (cadr x)))
                   (else
                     (expr (cadr x))
                     (emit (car x))
                     (expr (caddr x))))
             (emit ")"))
            (else
              (emit (car x) "(")
              (expr-list (cdr x))
              (emit ")"))))))

(define (type x)
  (case x
    ((...) (emit "..."))  ; for vararg function type
    ((void short long int size_t ssize_t float double
           C_PTABLE_ENTRY) 
     (emit x))
    ((char) (emit "C_char"))
    ((uchar) (emit "C_uchar"))
    ((ushort) (emit "unsigned short"))
    ((ulong) (emit "unsigned long"))
    ((uint) (emit "unsigned int"))
    ((u32) (emit "C_u32"))
    ((s32) (emit "C_s32"))
    ((u64) (emit "C_u64"))
    ((s64) (emit "C_s64"))
    ((ptr) (emit "void *"))
    ((word) (emit "C_word"))
    (else
      (case (and (pair? x) (car x))
        ((ptr) 
         (type (cadr x))
         (emit "*"))
        ((ref) 
         (type (cadr x))
         (emit "&"))
        ((template-instance)
         (type (cadr x))
         (emit "<")
         (emit-list (cddr x) type)
         (emit "<"))
        ((struct union enum)
         (emit (car x) " ")
         (type (cadr x)))
        ((function)
         (type (cadr x))
         (when (caddr x) (emit (caddr x)))
         (emit "(*)(")
         (emit-list (cdddr x) type)
         (emit ")"))
        (else (bomb "bad type" x))))))

)
